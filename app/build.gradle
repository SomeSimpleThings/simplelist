apply plugin: 'com.android.application'

def release_store_password
def release_key_password
def release_key_alias
def keystoreProperties = new Properties()
if (rootProject.file("keystore.properties").exists()) {
    keystoreProperties.load(new FileInputStream(rootProject.file("keystore.properties")))
    release_store_password = keystoreProperties['RELEASE_STORE_PASSWORD']
    release_key_password = keystoreProperties['RELEASE_KEY_PASSWORD']
    release_key_alias = keystoreProperties['RELEASE_KEY_ALIAS']
} else {
    release_store_password = System.env.RELEASE_STORE_PASSWORD
    release_key_password = System.env.RELEASE_KEY_PASSWORD
    release_key_alias = System.env.RELEASE_KEY_ALIAS
}
android {

    signingConfigs {
        release {
            storeFile file("simpleinc.jks")
            storePassword = release_store_password
            keyAlias = release_key_alias
            keyPassword = release_key_password
        }
    }
    compileSdkVersion 30
    defaultConfig {
        signingConfig signingConfigs.debug
        applicationId "com.somethingsimple.simplelist"
        minSdkVersion 23
        targetSdkVersion 30
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation":
                                     "$projectDir/schemas".toString()]
            }
        }
    }
    sourceSets {
        androidTest.assets.srcDirs +=
                files("$projectDir/schemas".toString())
    }
    buildTypes {
        release {
            minifyEnabled false
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            signingConfig signingConfigs.debug
        }
    }
    buildToolsVersion '29.0.3'
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    productFlavors {
    }
    buildFeatures {
        dataBinding = true
    }
    ndkVersion '21.3.6528147'
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.2'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation "androidx.navigation:navigation-fragment:2.3.1"
    implementation "androidx.navigation:navigation-ui:2.3.1"
    implementation 'androidx.preference:preference:1.1.1'

    implementation 'com.google.android.material:material:1.3.0-alpha03'

    //room
    implementation 'androidx.room:room-runtime:2.2.5'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    annotationProcessor "androidx.room:room-compiler:2.2.5"
    //dagger
    implementation 'com.google.dagger:dagger:2.25.2'
    implementation 'com.google.dagger:dagger-android:2.25.2'
    implementation 'com.google.dagger:dagger-android-support:2.25.2'
    annotationProcessor 'com.google.dagger:dagger-compiler:2.25.2'
    annotationProcessor 'com.google.dagger:dagger-android-processor:2.25.2'
    //firebase
    implementation 'com.google.android.gms:play-services-auth:18.1.0'
    implementation 'com.google.firebase:firebase-auth:19.4.0'
    implementation 'com.google.firebase:firebase-core:17.5.1'
    //retrofit
    implementation 'com.squareup.retrofit2:retrofit:2.6.2'
    implementation 'com.squareup.retrofit2:converter-gson:2.6.2'
    implementation 'com.squareup.okhttp3:logging-interceptor:4.2.2'
    //glide
    implementation 'com.github.bumptech.glide:glide:4.10.0'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.10.0'
    //debugdb
    debugImplementation 'com.amitshekhar.android:debug-db:1.0.6'

    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.3.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
    implementation fileTree(include: ['*.jar'], dir: 'libs')

}
apply plugin: 'com.google.gms.google-services'
